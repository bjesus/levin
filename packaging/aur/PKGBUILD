# Maintainer: Yoav Moshe <levin@yoavmoshe.com>
pkgname=levin
pkgver=0.0.2
pkgrel=1
pkgdesc="Background torrent seeding client for Anna's Archive"
arch=('x86_64')
url="https://github.com/bjesus/levin"
license=('MIT')
depends=('curl' 'openssl')
makedepends=('cmake' 'gcc' 'boost')
source=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
    cd "${pkgname}-${pkgver}"
    cmake -B build \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLEVIN_BUILD_TESTS=OFF \
        -DLEVIN_USE_STUB_SESSION=OFF
    cmake --build build
}

package() {
    cd "${pkgname}-${pkgver}"

    # Binary
    install -Dm755 build/platforms/linux/levin \
        "${pkgdir}/usr/bin/levin"

    # Systemd user service
    install -Dm644 packaging/systemd/levin.service \
        "${pkgdir}/usr/lib/systemd/user/levin.service"
}
